(ns hupo.main
  (:require ["package:flutter/material.dart" :as m]
            ["package:flutter/widgets.dart" :as w]
            [cljd.flutter :as f]))

(def state
  (atom {}))

(def board-size-x 3)
(def board-size-y 5)
(def cell-size 80)

(defn cell-widget [x y]
  (w/Container
   .width  cell-size
   .height cell-size
   .decoration (w/BoxDecoration
                .color (if (even? (+ x y)) m/Colors.white m/Colors.grey))))

(defn get-pawn-position [pawns selected-pawn]
  (let [[color id] selected-pawn]
    (get-in pawns [color id])))

(defn increase-turn-no [state]
  (if (and (-> state :finished :white)
           (-> state :finished :black))
    (-> state
        (update :turn-no inc)
        (assoc :finished {:white false :black false}))
    state))

(defn change-turn [state]
  (let [color (:current-player state)
        opposite-color (if (= color :white) :black :white)]
    (-> state
        (assoc :current-player opposite-color)
        (assoc-in [:finished color] true)
        (increase-turn-no))))

(defn advance-turn-phase [state]
  (let [advance-fn (fn [m] (let [turn-phase (:turn-phase m)
                                 next-phase (case turn-phase
                                              :designated-move :optional-move
                                              :optional-move :select-opponent-piece
                                              :select-opponent-piece :designated-move)
                                 last-turn (= turn-phase :select-opponent-piece)]
                             (cond-> m
                               :always (assoc :turn-phase next-phase)
                               last-turn change-turn)))]
    (advance-fn state)))

(defn check-winning-pawn [state]
  (let [{:keys [current-player pawns]} state
        current-pawns (get pawns current-player)
        winning-pos (if (= current-player :white)
                      [1 3]
                      [1 1])]
    (reduce (fn [s [id pos]]
              (if (= pos winning-pos)
                (assoc s :winner-pawn [current-player id])
                s))
            state
            current-pawns)))

(defn valid-tile? [pawns x y]
  (and (>= x 0) (< x board-size-x)
       (>= y 0) (< y board-size-y)
       (not (= [1 2] [x y]))
       (not (some (fn [[_ positions]]
                    (some (fn [[_ [px py]]]
                            (and (= px x) (= py y)))
                          positions))
                  pawns))))

(defn get-valid-tile [x y selected-pawn pawns]
  (let [[sx sy] (get-pawn-position pawns selected-pawn)
        nx (int (/ x cell-size))
        ny (int (/ y cell-size))]
    (when (and (or (and (<= (abs (- nx sx)) 1)
                        (<= (abs (- ny sy)) 0.5))
                   (and (<= (abs (- nx sx)) 0.5)
                        (<= (abs (- ny sy)) 1)))
               (valid-tile? pawns nx ny))
      [nx ny])))

(defn get-valid-moves [pawns x y]
  (let [possible-moves [[0 1] [0 -1] [1 0] [-1 0]]]
    (->> possible-moves
         (map (fn [[dx dy]] [(+ x dx) (+ y dy)]))
         (filter (fn [pm] (apply #(valid-tile? pawns %1 %2) pm))))))

(defn remove-blocked-pawn [{:keys [selected-pawn pawns] :as state}]
  (when (vector? selected-pawn)
    (let [[sx sy] (get-in pawns selected-pawn)
          [color id] selected-pawn
          valid-moves (get-valid-moves pawns sx sy)]
      (if (empty? valid-moves)
        (-> state
            (update-in [:pawns color] dissoc id)
            (assoc :selected-pawn nil)
            (assoc :fixed-pawn nil))
        state))))

(defn move-pawn [state x y]
  (let [{:keys [pawns selected-pawn]} state
        new-pos (get-valid-tile x y selected-pawn pawns)]
    (if new-pos
      (let [[color id] selected-pawn
            new-state (-> state
                          (assoc-in [:pawns color id] new-pos)
                          (assoc :last-moved-pawn selected-pawn)
                          (assoc :selected-pawn nil)
                          (assoc :fixed-pawn nil)
                          (check-winning-pawn)
                          (advance-turn-phase))]
        new-state)
      state)))

(defn pawn-widget [color selected?]
  (w/Container
   .width 60
   .height 60
   .decoration (w/BoxDecoration
                .color (cond
                         selected? m/Colors.red
                         (= color :white) m/Colors.green
                         :else m/Colors.black)
                .shape w/BoxShape.circle)))

(defn message-box []
  (f/widget
   :watch [s state]
   (let [{:keys [current-player turn-phase pawns selected-pawn fixed-pawn winner-pawn
                 last-moved-pawn turn-no finished]} s]
     (m/Card
      .elevation 8
      .margin (w/EdgeInsets.all 16)
      .child (w/Text
              (str "Current Player: " (name current-player)
                   "\nTurn Phase: " turn-phase
                   "\nSelected Pawn: " selected-pawn
                   "\n Last Moved Pawn: " last-moved-pawn
                   "\nFixed Pawn:" fixed-pawn
                   "\nPositions W: " (:white pawns)
                   "\nPositions B:" (:black pawns)
                   "\nFinished: " finished
                   "\nTurn Number:" turn-no
                   "\nWinner Pawn: " winner-pawn
                   "\nValid Moves: " (when selected-pawn
                                       (apply #(get-valid-moves pawns %1 %2)
                                              (get-in pawns selected-pawn))))
              .style (w/TextStyle
                      .fontSize 18
                      .fontWeight w/FontWeight.bold))))))

(defn first-turn-rule-white? [current-player turn-no turn-phase id]
  (if (and (= turn-no 1) (= current-player :white) (= turn-phase :designated-move))
    (= id :b)
    true))

(defn last-and-to-be-selected-different? [state selected]
  (not= (:last-moved-pawn state) selected))

(defn select-pawn [state color id]
  (let [{:keys [current-player turn-no turn-phase fixed-pawn]} state]
    (if-not fixed-pawn
      (cond
        (and (= turn-phase :select-opponent-piece)
             (not= current-player color))
        (-> state
            (assoc :selected-pawn [color id])
            (assoc :fixed-pawn [color id])
            (remove-blocked-pawn)
            (advance-turn-phase))

        (and (= current-player color)
             (#{:designated-move :optional-move} turn-phase)
             (last-and-to-be-selected-different? state [color id])
             (first-turn-rule-white? current-player turn-no turn-phase id))
        (assoc state :selected-pawn [color id])

        :else state)
      state)))

(defn first-turn-rule-black? [turn-no turn-phase]
  (and (not= turn-no 1) (= turn-phase :optional-move)))

(defn skip-turn-button [state]
  (m/TextButton
   .onPressed #(swap! state advance-turn-phase)
   .child (m/Text "Skip turn!")))

(defn board-widget []
  (f/widget
   :watch [s state]
   (let [{:keys [pawns selected-pawn turn-no turn-phase]} s
         [selected-color selected-id] selected-pawn]
     (w/Container
      .width 640 ;;240 without box
      .height 500
      .child
      (m/Column
       .children
       [(m/Row
         .children
         [(w/Stack
           .children
           (apply conj
                  [(m/Row
                    .children
                    (for [x (range board-size-x)]
                      (m/Column
                       .children
                       (for [y (range board-size-y)]
                         (w/GestureDetector
                          .onTap #(when selected-pawn
                                    (swap! state move-pawn (* x cell-size) (* y cell-size)))
                          .child (cell-widget x y))))))]
                  (for [[color positions] pawns
                        [id [x y]] positions]
                    (let [selected? (and (= selected-color color)
                                         (= selected-id id))]
                      (f/widget
                       :key (str color "-" id)
                       (w/AnimatedPositioned
                        .left (* x cell-size)
                        .top (* y cell-size)
                        .duration (m/Duration. .seconds 1)
                        .child (w/GestureDetector
                                .onTap #(swap! state select-pawn color id)
                                .child (pawn-widget color selected?))))))))
          (message-box)])
        (if (first-turn-rule-black? turn-no turn-phase)
          (skip-turn-button state)
          (w/Container))])))))

(defn main []
  (let [initial-state {:pawns {:white {:a [0 0]
                                       :b [1 0]
                                       :c [2 0]}
                               :black {:a [0 4]
                                       :b [1 4]
                                       :c [2 4]}}
                       :selected-pawn nil
                       :last-moved-pawn nil
                       :fixed-pawn nil
                       :current-player :white
                       :turn-phase :designated-move
                       :winner-pawn nil
                       :finished {:white false
                                  :black false}
                       :turn-no 1}]
    (reset! state initial-state)
    (f/run
     (m/MaterialApp
      .title "Hupo"
      .home (m/Scaffold
             .appBar (m/AppBar .title (m/Text "Hra"))
             .body  (m/Center
                     .child
                     (board-widget)))))))
